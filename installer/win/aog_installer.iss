; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "aog"
#define MyAppVersion "0.4"
#define MyAppPublisher "Intel, Inc."
#define MyAppURL "https://github.com/intel/aog"
#define MyAppExeName "aog.exe"
#define MyAppAssocName MyAppName + " File"
#define MyAppAssocExt ".myp"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt
#define MyAppDir "PATH/TO/AOG"

#include "environment.iss"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A305F2AA-0055-4595-9871-CA2A68DCD7AB}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={userpf}\{#MyAppName}
DisableDirPage=no
; "ArchitecturesAllowed=x64compatible" specifies that Setup cannot run
; on anything but x64 and Windows 11 on Arm.
ArchitecturesAllowed=x64compatible
; "ArchitecturesInstallIn64BitMode=x64compatible" requests that the
; install be done in "64-bit mode" on x64 or Windows 11 on Arm,
; meaning it should use the native 64-bit Program Files directory and
; the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64compatible
ChangesAssociations=yes
DisableProgramGroupPage=yes
DiskSpanning=no
; Uncomment the following line to run in non administrative install mode (install for current user only.)
PrivilegesRequired=lowest
OutputDir=output
OutputBaseFilename=aog-setup
Compression=lzma
SolidCompression=yes
WizardStyle=modern
;SignTool={#MySignTools}
;SignedUninstaller=true

[Files]
Source: "{#MyAppDir}\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion

[Tasks]
Name: envPath; Description: "Add installation directory to system PATH"; Flags: checkedonce
Name: runServer; Description: "Start aog server after installation"; Flags: unchecked

[Code]
procedure CurStepChanged(CurStep: TSetupStep);
var
  appPath: String;
  ErrorCode: Integer;
begin
  if CurStep = ssPostInstall then
  begin
    if WizardIsTaskSelected('envPath') then
    begin
      EnvAddPath(ExpandConstant('{app}'));
    end;

    if WizardIsTaskSelected('runServer') then
    begin
      appPath := ExpandConstant('{app}\{#MyAppExeName}');

      if not Exec(appPath, 'server start -d', '', SW_HIDE, ewNoWait, ErrorCode) then
      begin
        MsgBox('无法启动 aog server. 请手动运行 "aog server start -d".' + #13#10 + '错误代码: ' + IntToStr(ErrorCode), mbError, MB_OK);
      end;
    end;
  end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usPostUninstall then
    EnvRemovePath(ExpandConstant('{app}'));
end;